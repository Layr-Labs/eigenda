// SPDX-License-Identifier: UNLICENSED

pragma solidity ^0.8.9;

import {BN254} from "eigenlayer-middleware/libraries/BN254.sol";

/**
 * @title Library for verifying KZG commitments.
 * @author Layr Labs, Inc.b
*/
library EigenDAKZGUtils {
    using BN254 for BN254.G1Point;

    /**
     * @notice opens the KZG commitment at a point
     * @param point the point to evaluate the polynomial at
     * @param evaluation the evaluation of the polynomial at the point
     * @param tau the power of tau
     * @param commitment the commitment to the polynomial
     * @param proof the proof of the commitment
     */
    function openCommitment(
        uint256 point, 
        uint256 evaluation,
        BN254.G1Point memory tau, 
        BN254.G1Point memory commitment, 
        BN254.G2Point memory proof 
    ) internal view returns(bool) {
        BN254.G1Point memory negGeneratorG1 = BN254.generatorG1().negate();

        //e([s]_1 - w[1]_1, [pi(x)]_2) = e([p(x)]_1 - p(w)[1]_1, [1]_2)
        return BN254.pairing(
            tau.plus(negGeneratorG1.scalar_mul(point)), 
            proof, 
            commitment.plus(negGeneratorG1.scalar_mul(evaluation)), 
            BN254.negGeneratorG2()
        );
    }

}
